apply plugin: 'base'

allprojects {
	version = '1.3.0'
	ext.title = 'Events4J API'
	ext.vendor = 'object-zoo - Tilmann Kuhn'
	ext.copy = '2010 - 2014'
	ext.javadoc = 'http://docs.oracle.com/javase/8/docs/api/'
	ext.encoding = 'UTF-8'
}

subprojects {
	apply plugin: 'java'
	sourceCompatibility = 8
    targetCompatibility = 8
	
	repositories {
		mavenCentral()
	}
	
	dependencies {
		testCompile 'org.hamcrest:hamcrest-core:1.3'
		testCompile 'org.hamcrest:hamcrest-library:1.3'
		testCompile 'junit:junit:4.11'
		testCompile 'org.mockito:mockito-all:1.9.5'
	}

	jar	{
		manifest {
			attributes("Implementation-Title": project.title, "Implementation-Version": project.version, "Implementation-Vendor": project.vendor)
		}
	}
	
	task updateTestResultTimestamps {
		dependsOn test
		inputs.files test.outputs.files
		doLast {
			def timestamp = System.currentTimeMillis()
			test.testResultsDir.eachFile { it.lastModified = timestamp }
			test.testReportDir.eachFile { it.lastModified = timestamp }
		}
    }
	
	task jenkinsBuild {
		dependsOn updateTestResultTimestamps
	}
}
 
project(':events4j') {
	jar	{
		from sourceSets.main.allJava
	}
}

project(':events4j-async') {
	dependencies {
		compile project(':events4j')
	}
}

task javadoc(type: Javadoc) {
	destinationDir = new File("${rootDir}/docs/api")
    source = files(subprojects.collect { project -> project.sourceSets.main.allJava })
    classpath = files(subprojects.collect { project -> project.sourceSets.main.compileClasspath })
}

task distZip(type: Zip) {
	dependsOn ':events4j:build'
	dependsOn ':events4j-async:build'
	dependsOn ':javadoc'

	into("${baseName}-${version}") {
		from (rootDir)
		{
			include '*.*'
			exclude { file -> file.directory }
			exclude '*.jar'
		}
		from (rootDir)
		{
			include 'docs/**/*', 'events4j/**/*', 'events4j-async/**/*', 'lib/**/*'
			exclude '*/**/build', '*/**/generated'
		}
		from (files (subprojects.collect { project -> project.jar.destinationDir }))
		{
			include '*.jar'
		}
	}
}

allprojects {
	javadoc	{
		options {
			setUse(true)
			setAuthor(true)
			setVersion(true) 
			setNoTimestamp(true)
			encoding project.encoding
			links project.javadoc
			docTitle "<h1>${project.title} ${project.version}</h1>"
			windowTitle "${project.title} ${project.version}"
		}
	}
}